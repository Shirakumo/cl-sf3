(in-package #:org.shirakumo.sf3)

(bs:define-io-alias pixel-format
    (case uint8
      (#x01 'bs:sint8)
      (#x02 'bs:sint16)
      (#x04 'bs:sint32)
      (#x08 'bs:sint64)
      (#x11 'bs:uint8)
      (#x12 'bs:uint16)
      (#x14 'bs:uint32)
      (#x18 'bs:uint64)
      (#x22 'bs:float16)
      (#x24 'bs:float32)
      (#x28 'bs:float64)))

(bs:define-io-structure (image (:constructor %make-image))
  (width uint32)
  (height uint32)
  (depth uint32)
  (channels uint8)
  (format pixel-format)
  (data (vector (case (bs:slot format)
                  (#x01 bs:sint8)
                  (#x02 bs:sint16)
                  (#x04 bs:sint32)
                  (#x08 bs:sint64)
                  (#x11 bs:uint8)
                  (#x12 bs:uint16)
                  (#x14 bs:uint32)
                  (#x18 bs:uint64)
                  (#x22 bs:uint16)
                  (#x24 bs:float32)
                  (#x28 bs:float64))
                (* (bs:slot width)
                   (bs:slot height)
                   (bs:slot depth)
                   (ldb (byte 4 0) (bs:slot channels))))))

(defun make-image (pixels width height &key (depth 1) (channel-format :v))
  (%make-image :width width
               :height height
               :depth depth
               :channels (ecase channel-format
                           (:V #x01)
                           (:VA #x02)
                           (:RGB #x03)
                           (:RGBA #x04)
                           (:AV #x12)
                           (:BGR #x13)
                           (:ABGR #x14)
                           (:ARGB #x24)
                           (:BGRA #x34)
                           (:CMYK #x44)
                           (:KYMC #x54))
               :format (lisp-type->bs-type (array-element-type pixels))
               :data pixels))

(defun pixel-type (image)
  (ecase (image-channels image)
    (#x01 :V)
    (#x02 :VA)
    (#x03 :RGB)
    (#x04 :RGBA)
    (#x12 :AV)
    (#x13 :BGR)
    (#x14 :ABGR)
    (#x24 :ARGB)
    (#x34 :BGRA)
    (#x44 :CMYK)
    (#x54 :KYMC)))

(define-print-method image "~d~[~;~:;~:*x~d~]~[~;~:;~:*x~d~] ~a ~a" 
  width height depth format (pixel-type object))

(define-accessors image width height depth channels data)
